plugins {
    id 'java'
    id 'maven-publish'
}

group = 'fr.orion'
version = '1.0.0'

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    withJavadocJar()
    withSourcesJar()
}

repositories {
    mavenCentral()
}

dependencies {
    implementation("net.dv8tion:JDA:5.5.1")
    implementation("org.yaml:snakeyaml:2.4")

    testImplementation 'org.junit.jupiter:junit-jupiter:5.10.2'
    testImplementation 'org.mockito:mockito-core:5.11.0'
    testImplementation 'org.assertj:assertj-core:3.25.3'
}

test {
    useJUnitPlatform()

    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
}

compileJava.options.encoding = 'UTF-8'
compileTestJava.options.encoding = 'UTF-8'
javadoc.options.encoding = 'UTF-8'
javadoc.options.charSet = 'UTF-8'

javadoc {
    options {
        encoding = 'UTF-8'
        charSet = 'UTF-8'
        author = true
        version = true
        use = true
        windowTitle = "Orion Discord Bot API"
        docTitle = "Orion Discord Bot API"
        addStringOption('Xdoclint:none', '-quiet')
    }
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = 'fr.orion'
            artifactId = 'orion-api'
            version = '1.0.0'
            from components.java

            pom {
                name = 'Orion Discord Bot API'
                description = 'Clean, modular Discord bot API framework'
                url = 'https://github.com/Arinonia/Orion-API'
            }
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/Arinonia/Orion-API"
            credentials {
                username = project.findProperty("gpr.user") ?: System.getenv("USERNAME")
                password = project.findProperty("gpr.key") ?: System.getenv("TOKEN")
            }
        }
    }
}